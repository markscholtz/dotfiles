# setopt XTRACE VERBOSE

# Your project folder that we can `c [tab]` to directories in this folder. This
# is made possible by the _c function in zsh/functions.
export PROJECTS=~/code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# Find all zsh files in the repository.
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# Load the path files.
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# Load everything but the path and completion files.
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# Initialize autocomplete here, otherwise functions won't be loaded.
autoload -U compinit
compinit

# load every completion after autocomplete loads.
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files
