" Modeline and Notes {
" vim: set foldmarker={,} foldlevel=0 foldmethod=marker :
"
" I have copied a lot of Steve Francia's .vimrc file which can
" be found here: http://spf13.com/post/ultimate-vim-config
"
" }

set nocompatible " must be first line

" Configure minpac plugin management {

  " Load and initialize minpac and define plugins to be installed.
  function! PackInit() abort
    packadd minpac

    call minpac#init()
    " minpac must have {'type': 'opt'} so that it can be loaded with
    " `packadd`.
    call minpac#add('k-takata/minpac', {'type': 'opt', 'rev': 'v3.0.0'})

    " Additional plugins.
    call minpac#add('MarcWeber/vim-addon-mw-utils')
    call minpac#add('Quramy/tsuquyomi')
    call minpac#add('SirVer/ultisnips')
    call minpac#add('bling/vim-airline')
    call minpac#add('godlygeek/tabular')
    call minpac#add('honza/vim-snippets')
    call minpac#add('junegunn/fzf.vim')
    call minpac#add('kana/vim-vspec')
    call minpac#add('kchmck/vim-coffee-script')
    call minpac#add('leafgarland/typescript-vim')
    call minpac#add('lifepillar/vim-solarized8', {'rev': 'v1.2.0'})
    call minpac#add('markscholtz/vim-folding-rspec')
    call minpac#add('mileszs/ack.vim')
    call minpac#add('nelstrom/vim-markdown-folding')
    call minpac#add('pangloss/vim-javascript')
    call minpac#add('preservim/nerdtree', {'rev': '6.9.11'})
    call minpac#add('scrooloose/syntastic')
    call minpac#add('sjl/gundo.vim')
    call minpac#add('sjl/splice.vim')
    call minpac#add('skalnik/vim-vroom')
    call minpac#add('tomtom/tlib_vim')
    call minpac#add('tpope/vim-bundler')
    call minpac#add('tpope/vim-commentary')
    call minpac#add('tpope/vim-dispatch')
    call minpac#add('tpope/vim-endwise')
    call minpac#add('tpope/vim-fugitive', {'rev': 'v3.2'})
    call minpac#add('tpope/vim-markdown')
    call minpac#add('tpope/vim-obsession')
    call minpac#add('tpope/vim-rails')
    call minpac#add('tpope/vim-rake')
    call minpac#add('tpope/vim-repeat')
    call minpac#add('tpope/vim-scriptease', {'type': 'opt'})
    call minpac#add('tpope/vim-surround')
    call minpac#add('tpope/vim-unimpaired')
    call minpac#add('vim-scripts/JSON.vim')
    call minpac#add('vim-scripts/matchit.zip')
    call minpac#add('vim-scripts/taglist.vim')
    call minpac#add('zsh-users/zsh-completions')
  endfunction

  " Define user commands for updating/cleaning the plugins.
  " Each of them calls PackInit() to load minpac and register the information
  " of plugins, then performs the task.
  command! PackUpdate source $MYVIMRC | call PackInit() | call minpac#update()
  command! PackClean  source $MYVIMRC | call PackInit() | call minpac#clean()
  command! PackStatus packadd minpac | call minpac#status()
" }

" Basics {
    set hidden          " Not sure what this does, but apparently it should be a default setting
" }

" General {
    filetype plugin indent on                       " Automatically detect file types.
    syntax enable                                   " syntax highlighting
    set encoding=utf-8
    scriptencoding utf-8
    set autowrite
    set viewoptions=cursor,folds,options,slash,unix " better unix / windows compatibility
    set history=1000                                " Store a ton of history (default is 20)
    set nospell                                     " spell checking off
    set spellfile=~/.vim/spell/en.utf-8.add         " spell checking off
    set visualbell                                  " don't beep
    set noerrorbells                                " don't beep
    set splitbelow                                  " make window splitting feel more natural
    set splitright                                  " make window splitting feel more natural

    " Set clipboard unless running in tmux: http://stackoverflow.com/questions/11404800/fix-vim-tmux-yank-paste-on-unnamed-register
    if $TMUX == ''
      set clipboard+=unnamed
    endif
    source ~/.tripboard/tripboard.vim

    " Setting up the directories {
        set backup                        " backups are nice ...
        set backupdir=$HOME/.vim/tmp/backup// " but not when they clog
        set directory=$HOME/.vim/tmp/swap//   " same for swap files
        set viewdir=$HOME/.vim/tmp/view//     " same for view files

        " Creating directories if they don't exist
        silent execute '!mkdir -p $HOME/.vim/tmp/backup'
        silent execute '!mkdir -p $HOME/.vim/tmp/swap'
        silent execute '!mkdir -p $HOME/.vim/tmp/view'
        " if has('autocmd') "remain compatible with older versions of Vim that do not have the autocmd functions
        "   autocmd BufWinEnter * silent! loadview      "make vim load view (state) (folds, cursor, etc)
        "   autocmd BufWinLeave * silent! mkview        "make vim save view (state) (folds, cursor, etc)
        " endif
    " }
" }

" Filetype Specifics {

    if has('autocmd')
      autocmd FileType help set number relativenumber

      augroup set_filetypes
        autocmd!
        autocmd BufNewFile,BufRead *.json    setl filetype=json
        autocmd BufNewFile,BufRead *.less    setl filetype=css
        autocmd BufNewFile,BufRead *.md      setl filetype=markdown
        autocmd BufNewFile,BufRead Guardfile setl filetype=ruby
        autocmd BufNewFile,BufRead *.coffee  setl foldmethod=indent nofoldenable
      augroup END
    endif
" }

" Vim UI {

    " Set up dark solarized color scheme using true-color (24-bit color).
    " This will only work if using a terminal supporting true-color.
    " See https://github.com/lifepillar/vim-solarized8 for details.
    set termguicolors
    let &t_8f = "\<Esc>[38;2;%lu;%lu;%lum"
    let &t_8b = "\<Esc>[48;2;%lu;%lu;%lum"
    set background=dark
    color solarized8

    set showmode    " display the current mode

    set cursorline  " highlight current line

    if has('cmdline_info')
      set ruler                                          " show the ruler
      set rulerformat=%30(%=\:b%n%y%m%r%w\ %l,%c%V\ %P%) " a ruler on steroids
      set showcmd                                        " show partial commands in status line and
    endif

    if has('statusline')
      set laststatus=2                                      " always show statusline
      " Use the commented line if fugitive isn't installed
      "set statusline=%<%f\ %=\:\b%n%y%m%r%w\ %l,%c%V\ %P   " a statusline, also on steroids
      set statusline=%<%f\ %h%m%r%{fugitive#statusline()}%=%-14.(%l,%c%V%)\ %P
    endif

    " Use a bar-shaped cursor for insert mode, even through tmux.
    if exists('$TMUX')
        let &t_SI = "\<Esc>Ptmux;\<Esc>\<Esc>]50;CursorShape=1\x7\<Esc>\\"
        let &t_EI = "\<Esc>Ptmux;\<Esc>\<Esc>]50;CursorShape=0\x7\<Esc>\\"
    else
        let &t_SI = "\<Esc>]50;CursorShape=1\x7"
        let &t_EI = "\<Esc>]50;CursorShape=0\x7"
    endif

    set backspace=indent,eol,start " backspace for dummys
    set linespace=0                " No extra spaces between rows
    set number                     " Line numbers on
    set relativenumber             " Relative line numbers on (as of Vim 7.4 both numbering styles can be set for a hybrid mode)
    set showmatch                  " show matching brackets/parenthesis
    set incsearch                  " find as you type search
    set hlsearch                   " highlight search terms
    set ignorecase                 " case insensitive search
    set smartcase                  " case sensitive when uc present
    set winminheight=0             " windows can be 0 line high
    set wildmenu                   " show list instead of just completing
    set wildmode=list:longest,full " comand <Tab> completion, list matches, then longest common part, then all.
    set whichwrap=b,s,h,l,<,>,[,]  " backspace and cursor keys wrap to
    set scrolloff=3                " minimum lines to keep above and below cursor
    set foldenable                 " auto fold code
    set gdefault                   " the /g flag on :s substitutions by default
" }

" Formatting {
    set nowrap                        " turn text wrap off by default
    set autoindent                    " indent at the same level of the previous line
    set tabstop=2                     " an indentation every two columns
    set softtabstop=2                 " when backspacing treat spaces created by expandtab as tabs
    set shiftwidth=2                  " use indents of 2 spaces
    set shiftround                    " round indentations to a multiple of shiftwidth
    set expandtab                     " tabs are expanded to spaces
    set matchpairs+=<:>               " match, to be used with %

    " Highlight the column just after the textwidth setting for a file.
    if exists('+colorcolumn')
      set colorcolumn=+1
    endif

    " Show all whitespace (from http://stackoverflow.com/questions/1675688/make-vim-show-all-white-spaces-as-a-character)
    set list
    set listchars=tab:▸\ ,trail:~,nbsp:~,extends:»,precedes:« " This shows spaces as ~ when using the 'set list' command
    " set listchars+=eol:¬                                      " uncomment this to enable displaying end of line characters
" }

" Key Mappings {

    " Change mapleader key
    let maplocalleader = ","

    " Make escaping from insert mode easier
    inoremap jk <esc>
    " inoremap <esc> <nop>

    " Easier moving in tabs and windows
    noremap <C-J> <C-W>j
    noremap <C-K> <C-W>k
    noremap <C-L> <C-W>l
    noremap <C-H> <C-W>h
    noremap <S-H> gT
    noremap <S-L> gt

    " Easier moving between wrapped lines
    nnoremap <silent> j gj
    nnoremap <silent> k gk

    " Tripboard
    map <LocalLeader>c "ay:call TripboardCopy()<CR>
    map <LocalLeader>v :call TripboardPaste()<CR>"ap

    " Toggle a fold
    noremap <Space> za

    " Enable number incrementing in tmux
    noremap <C-b> <C-a>

    " Clear the search buffer (http://nvie.com/posts/how-i-boosted-my-vim/)
    nnoremap <silent> <LocalLeader><Space> :nohlsearch<CR>

    " Shortcut to edit my .vimrc
    nnoremap <silent> <LocalLeader>ev :e $MYVIMRC<cr>

    " Reload my .vimrc to apply any changes made
    nnoremap <silent> <LocalLeader>sv :so $MYVIMRC<cr>

" }

" Key Mappings - Plugin Specific {

  " Nerdtree {
    " Toggle NERDTree visibility
    nnoremap <silent> <LocalLeader>n :NERDTreeToggle<CR>

    " Find the current file in NERDTree
    nnoremap <silent> <LocalLeader>l :NERDTreeFind<CR>
  " }

  " Powerline {
    nnoremap <LocalLeader>g :GundoToggle<CR>
  " }

  " Taglist {
    nnoremap <silent> <LocalLeader>t :TlistToggle<CR>
  " }

  " Vimux {
    " Run the current file with rspec
    noremap <LocalLeader>rr :call RunVimTmuxCommand("clear; bundle exec rspec " . bufname("%"))<CR>

    " Prompt for a command to run
    noremap <LocalLeader>rp :PromptVimTmuxCommand<CR>

    " Run last command executed by RunVimTmuxCommand
    noremap <LocalLeader>rl :RunLastVimTmuxCommand<CR>

    " Inspect runner pane
    noremap <LocalLeader>ri :InspectVimTmuxRunner<CR>

    " Close vim tmux runner opened by RunVimTmuxCommand
    noremap <LocalLeader>rq :CloseVimTmuxRunner<CR>

    " Close all other tmux panes in current window
    noremap <LocalLeader>rx :CloseVimTmuxPanes<CR>

    " Interrupt any command running in the runner pane
    noremap <LocalLeader>rs :InterruptVimTmuxRunner<CR>
  " }

  " fzf.vim — https://github.com/junegunn/fzf.vim {
    nnoremap <silent> <LocalLeader>ff :Files<CR>
    nnoremap <silent> <LocalLeader>fs :Snippets<CR>
  " }

" }

" Functions {
    " Strip trailing whitespaces (vimcasts.org: episode #4)
    nnoremap <silent> <LocalLeader>sw :call <SID>StripTrailingWhitespaces()<CR>
    function! <SID>StripTrailingWhitespaces()
        " Preparation: save last search, and cursor position.
        let _s=@/
        let l = line(".")
        let c = col(".")
        " Do the business:
        %s/\s\+$//e
        " Clean up: restore previous search history, and cursor position
        let @/=_s
        call cursor(l, c)
    endfunction

    " This is my attempt at making a distraction free writing zone
    " TODO: It could do with some work
    noremap ,df :call DistractionFreeWriting ()<CR>
    function! DistractionFreeWriting ()
        exec ':set fuoptions='
        exec ':set fullscreen'
        exec ':set columns=80'
        exec ':set guioptions-=r'
        exec ':set textwidth=76'
        exec ':set nonumber'
        exec ':set norelativenumber'
    endfunction

    " Show syntax highlighting groups for word under cursor (vimcasts.org: episode #25)
    nnoremap <C-S-S> :call <SID>SynStack()<CR>
    function! <SID>SynStack()
      if !exists("*synstack")
        return
      endif
      echo map(synstack(line('.'), col('.')), 'synIDattr(v:val, "name")')
    endfunc

    " Set tabstop, softtabstop and shiftwidth to the same value (vimcasts.org: episode #2)
    command! -nargs=* Stab call Stab()
    function! Stab()
      let l:tabstop = 1 * input('set tabstop = softtabstop = shiftwidth = ')
      if l:tabstop > 0
        let &l:sts = l:tabstop
        let &l:ts = l:tabstop
        let &l:sw = l:tabstop
      endif
      call SummarizeTabs()
    endfunction

    function! SummarizeTabs()
      try
        echohl ModeMsg
        echon 'tabstop='.&l:ts
        echon ' shiftwidth='.&l:sw
        echon ' softtabstop='.&l:sts
        if &l:et
          echon ' expandtab'
        else
          echon ' noexpandtab'
        endif
      finally
        echohl None
      endtry
    endfunction
" }

" Configure Plugins {

  " Powerline {
      " let g:Powerline_symbols = 'fancy'
      " let g:Powerline_theme = 'skwp'
      " let g:Powerline_colorscheme = 'skwp'
      " call Pl#Theme#InsertSegment('ws_marker', 'after', 'lineinfo') "Add trailing whitespace indicator
  " }

  " Taglist {
      let Tlist_Use_Right_Window = 1
  " }

  " NERDTree {
      let NERDTreeChDirMode = 2
      let NERDTreeShowLineNumbers = 1
      " let NERDTreeHijackNetrw = 0
      let NERDTreeWinSize = 40
  " }

  " Vroom {
      let g:vroom_use_vimux = 1
  " }

  " UltiSnips {
      let g:UltiSnipsExpandTrigger = "<c-k>"
      let g:UltiSnipsListSnippets = "<c-l>"
      " let g:UltiSnipsJumpForwardTrigger = "<M-j>"
      " let g:UltiSnipsJumpBackwardTrigger = "<M-k>"
  " }

  " Ack {
      let g:ackprg = 'ag --nogroup --nocolor --column'
  " }

  " YouCompleteMe {
      " let g:ycm_auto_trigger = 0
      " let g:ycm_key_list_select_completion = []
      " let g:ycm_key_list_previous_completion = []
  " }

  " Syntastic {
      let g:syntastic_html_tidy_ignore_errors=[" proprietary attribute", "is not recognized!","discarding unexpected"]
  " }


  " FZF — https://github.com/junegunn/fzf {
      " This is required to load the basic vim plugin that comes with fzf.
      if isdirectory(expand("~/.fzf")) " Installed locally using git
        set runtimepath+=~/.fzf
      endif
      if isdirectory(expand("/usr/local/opt/fzf")) " Installed with Homebrew.
        set runtimepath+=/usr/local/opt/fzf
      endif
  " }

" }

" GUI Settings {
    " GVIM- (here instead of .gvimrc)
    if has('gui_running')
      set anti                              " antialias font
      set guioptions-=T                     " remove the toolbar
      set guioptions-=L                     " turn off left scrollbar
      set guioptions-=r                     " turn off right scrollbar
      "set guioptions+=rb                    " turn on right and horizontal scrollbars
      set lines=50                           " 50 lines of text instead of 24,
      set columns=200                       " 200 columns
    endif

    " Version specific settings {
        if has("gui_macvim")
          set guifont=Inconsolata:h14           " set font
          set fuoptions=maxvert,maxhorz         " fullscreen options (MacVim only), resized window when changed to fullscreen (max lines and columns)
        endif
    " }
" }

" Local overrides
  let $LOCALFILE=expand("~/.vimrc_local")
  if filereadable($LOCALFILE)
      source $LOCALFILE
  endif
" }
